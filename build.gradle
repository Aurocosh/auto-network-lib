import org.gradle.internal.os.OperatingSystem

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    mavenCentral()
}

//logging.level = LogLevel.DEBUG
def currentReleaseType = getReleaseType()
version = "${mod_version}.${generateVersionCode()}"
println version

group = mod_group
archivesBaseName = "${mod_archives_base_name}-${mc_sub_version}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "${mc_sub_version}-${forge_version}"
    runDir = "run"
    mappings = mc_mappings
//    makeObfSourceJar = false
}

dependencies {

}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

static def generateVersionCode() {
    def commit_base = "68b2f1c29aa3c5f27992437bf6db821958890077"
    def result = "git rev-list ${commit_base}..HEAD --count".execute().text.trim() //unix
    if(result.empty) throw new RuntimeException("Could not generate versioncode on this platform? Cmd output: ${result.text}")
    return result.toInteger()
}

static def getReleaseType() {
    def tags = shExec("git notes show", true, true)
    def isRelease = tags.contains('release')
    (isRelease) ? "release" : "beta"
}

static def generateChangeLog(releaseType) {
    def currentChange = shExec("git log --format=%B -n 1 HEAD")
    if (releaseType == 'beta')
        return currentChange

    def logWithNotes = shExec("git log --format=%H_%N")
    def matches = logWithNotes =~ '([0-9a-f]{5,40})_.*release'
    def lastReleaseHash = matches[1][1]

    def logFromPreviousBetas = shExec("git log --format=%s $lastReleaseHash..HEAD~1", true)
    return "Last change:\n$currentChange\n\nChangelog from previous beta versions:\n$logFromPreviousBetas"
}

static def shExec(command, ignoreEmptyResult = false, ignoreErrors = false) {
    if (OperatingSystem.current().isLinux())
        command = command.replaceAll('"', '\\\\"')

    def resultBuilder = '' << ''
    def errorsBuilder = '' << ''
    def task = command.execute()
    task.waitForProcessOutput(resultBuilder, errorsBuilder)

    def result = resultBuilder.toString().trim()
    def errors = errorsBuilder.toString().trim()
    if ((result.empty && !ignoreEmptyResult) || (!errors.empty && !ignoreErrors))
        throw new RuntimeException("Could not execute command $command. Cmd output: $result. Errors: $errors")
    return result
}

jar {

}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

task srcJar(type: Jar) {
    from(sourceSets.main.java)
    classifier = 'sources'
    archiveName = "${baseName}-${version}-sources.${extension}"
}

artifacts {
    archives deobfJar, srcJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.archivesBaseName
            version project.version

            artifact deobfJar {
                classifier 'deobf'
            }
            artifact srcJar {
                classifier 'sources'
            }
        }
    }

    repositories {
        maven {
            url "https://repo.repsy.io/mvn/aurocosh/default"
            credentials {
                username 'aurocosh'
                password System.getenv('REPSY_PASSWORD')
            }
        }
    }
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_API_KEY') ? System.getenv('CURSEFORGE_API_KEY') : ''
    project {
        id = curse_project_id
        changelog = generateChangeLog(currentReleaseType)
        releaseType = currentReleaseType
        addGameVersion mc_sub_version
    }
}